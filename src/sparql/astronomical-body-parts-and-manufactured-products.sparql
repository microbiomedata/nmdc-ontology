PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX envo: <http://purl.obolibrary.org/obo/ENVO_>
PREFIX def:  <http://purl.obolibrary.org/obo/IAO_0000115> 
PREFIX manufactured_product:  <http://purl.obolibrary.org/obo/ENVO_00003074>
PREFIX astronomical_body_part: <http://purl.obolibrary.org/obo/ENVO_01000813>

SELECT DISTINCT ?class ?label ?definition
WHERE
{
  {
    # find all astronomical body parts
    ?class rdfs:subClassOf* astronomical_body_part:;
		   rdfs:label ?class_label .
    BIND(STR(?class_label) as ?label) . # this remove language tags

    # get definition if present
    optional {
	  ?class def: ?class_def .
	  BIND(STR(?class_def) as ?definition) .
	}
  } UNION {
    # find all manufactured products
    ?class rdfs:subClassOf* manufactured_product:;
		   rdfs:label ?class_label .
    BIND(STR(?class_label) as ?label) . # this remove language tags

    # get definition if present
    optional {
	  ?class def: ?class_def .
	  BIND(STR(?class_def) as ?definition) .
	}
  }
}
ORDER BY ?label
